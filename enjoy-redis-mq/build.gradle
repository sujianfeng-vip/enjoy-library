def groupName = 'vip.sujianfeng'
def artifactName = "enjoy-redis-mq"
def versionName = '1.0.1'
apply plugin: 'java'
apply plugin: 'maven-publish'
apply plugin: 'signing'

sourceCompatibility = 1.8

dependencies {
    implementation 'vip.sujianfeng:enjoy-utils:1.0.1'
    implementation 'vip.sujianfeng:enjoy-redis:1.0.1'
    implementation 'redis.clients:jedis:2.9.0'
    implementation 'com.alibaba:fastjson:1.2.70'
    implementation 'org.slf4j:slf4j-api:1.7.25'

    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.8.1'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.8.1'
    testImplementation 'ch.qos.logback:logback-classic:1.2.3'
}

test {
    useJUnitPlatform()
}

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
}

task sourceJar(type: Jar) {
    from sourceSets.main.allJava
    classifier "sources"
}


task javaDocJar(type: Jar) {
    classifier "javadoc"
    from javadoc
}


publishing {
    publications {
        mavenJava(MavenPublication) {
            groupId groupName
            artifactId artifactName
            version versionName
            from components.java
            //配置上传源码
            artifact sourceJar {
                classifier "sources"
            }
            artifact javaDocJar {
                classifier "javadoc"
            }
            // https://docs.gradle.org/current/dsl/org.gradle.api.publish.maven.MavenPublication.html
            pom {
                name = artifactName
                description = "Enjoy the Java public tool library"
                url = "https://github.com/sujianfeng-vip/enjoy-library"
                licenses {
                    license {
                        name = "The Apache License, Version 2.0"
                        url = "https://www.apache.org/licenses/LICENSE-2.0.txt"
                    }
                }
                developers {
                    developer {
                        id = "sujianfeng"
                        name = "sujianfeng"
                        email = "360482507@qq.com"
                    }
                }
                scm {
                    connection = "scm:https://github.com/sujianfeng-vip/enjoy-library.git"
                    developerConnection = "scm:https://github.com/sujianfeng-vip/enjoy-library.git"
                    url = "https://github.com/sujianfeng-vip/enjoy-library"
                }
            }
        }
    }
    def publishUrl= ""
    if (versionName.endsWith("SNAPSHOT")){
        publishUrl = project.ext.get("publishUrl_snapshots")
    }else {
        publishUrl= project.ext.get("publishUrl_release")
    }
    repositories {
        // mavenLocal()
        maven {
            credentials {
                username System.getenv("sonatypeUsername")
                password System.getenv("sonatypePassword")
            }
            url publishUrl
        }
    }
}

//签名配置，注意这里的顺序，必须在 publishing 配置之后
signing {
    sign publishing.publications.mavenJava
}
